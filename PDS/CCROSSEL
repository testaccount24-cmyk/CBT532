<<CCROSSEL>> : A1=MEMBER NAME,
  DECLARE PTR SEL DESC NAME BASE CARD LIBR
  CREATE AWS ROSDATA NONRECOVERABLE LEVEL
  IF A1 EQ '$$CANCEL'
      LET CARD = '$$CANCEL               REPLACEMENT CANCELLED'
  ELSE
      LET NAME = A1
      DO <<GETLIST>>
      IF AWSCOUNT GT 0
          LET SEL = 1
          IF AWSCOUNT GT 1
              DO <<SHOLIST>>
          ENDIF
          IF SEL NE 999999
              FETCH $$CCTEMP
             %READ AWS %SEL% CARD
          ENDIF
          DELETE $$CCTEMP
      ELSE
          LET CARD = ''
      ENDIF
  ENDIF
  LET CARD = PAD(CARD 80)
  LET NAME = TRIM(SUBSTR(CARD 1 8))
  LET DESC = SUBSTR(CARD 24 30)
  DELETE
  RETURN NAME DESC
<<GETLIST>> PROC
  IF LENGTH(NAME) EQ 8 AND CONFORM(SUBSTR(NAME 7 2) 'N')
      LET BASE = SUBSTR(NAME 1 6)
  ELSE
      LET BASE = NAME
  ENDIF
  DELETE
  TRAP
 %ATTACH LIB %PREFIX%.%BASE%*
  TRAP OFF
  LET SEL = 0
  IF TC EQ 0
      COPY LIB T
      DETACH LIB
      RENUMBER 1 1
      DELETE 1 3
      RENUMBER 1 1
::::::LSHIFT 6
      READ AWS 1 CARD
      %LSHIFT %INDEX(CARD '.')%
      SORT SORT FIELDS=(1,8,CH,D)
      RENUMBER 1 1
      UPDATE $$CCTEMP
      LOOP PTR FROM 1 TO HIGHSEQ BY 1
         %READ AWS %PTR% CARD
          LET CARD = SUBSTR(CARD 1 8)
          IF CARD NE PAD(NAME 8) AND CARD NE PAD(BASE 8)
             %DELETE %PTR%
          ENDIF
      ENDLOOP
      IF AWSCOUNT EQ 0
          FETCH $$CCTEMP
      ENDIF
      RENUMBER 1 1
      UPDATE $$CCTEMP
      LET SEL = 1
  ENDIF
ENDPROC
<<SHOLIST>> PROC
  PANEL ACTIVATE PFX.CCROSPAN
 %LOOP PTR FROM 1 TO %HBOUND(P.SEL)% BY 1
    UNTIL PTR GT AWSCOUNT
     %READ AWS %PTR% CARD
      LET P.MEM<PTR>   = SUBSTR(CARD 1 8)
      LET P.LINES<PTR> = SUBSTR(CARD 57 5)
      LET P.DESC<PTR>  = SUBSTR(CARD 24 30)
  ENDLOOP
  PANEL SEND CURSOR P.SEL<1>
  LOOP
    UNTIL AID EQ 'PF1' OR AID EQ 'CLEAR' OR AID EQ 'ENTER'
      LET P.MSG<1> = 'INVALID PF KEY - TRY AGAIN'
      PANEL RESEND CURSOR P.SEL<1>
      LET P.MSG<1> = ''
  ENDLOOP
  SELECT FIRST
    WHEN AID EQ 'PF1'
      EXEC PFX.CCHLPSHO 'CCROS'
      SELECT AWS ROSDATA
      RELOOP
    WHEN AID EQ 'CLEAR'
      LET SEL = 999999
      LET CARD = '$$CANCEL               REPLACEMENT CANCELLED'
    WHEN NONE
      LET SEL = 0
  ENDSEL
  LOOP
    UNTIL SEL GT 0
     %LOOP PTR FROM 1 TO %HBOUND(P.SEL)% BY 1
        UNTIL PTR GT AWSCOUNT
          LET A.SEL<PTR> = 'UALM'
          LET CARD = TRIM(TRIM(P.SEL<PTR> '_'))
          IF CARD NE ''
              IF CARD EQ 'S' OR CARD EQ 'X'
                  IF SEL NE 0
                      LET P.MSG<2> = 'ONLY ONE MEMBER MAY BE SELECTED'
                      LET A.SEL<PTR> = 'UAHM'
                  ENDIF
              ELSE
                  LET P.MSG<2> = 'SELECTION CHARACTER MUST'
                  LET P.MSG<2> = P.MSG<2>|' BE "S" OR "X"'
                  LET A.SEL<PTR> = 'UAHM'
              ENDIF
              LET SEL = PTR
          ENDIF
      ENDLOOP
      IF SEL GT 0
          IF A.SEL<SEL> EQ 'UAHM'
              PANEL RESEND CURSOR P.SEL<SEL>
              LOOP
                UNTIL AID EQ 'PF1' OR AID EQ 'CLEAR' OR AID EQ 'ENTER'
                  LET P.MSG<1> = 'INVALID PF KEY - TRY AGAIN'
                  PANEL RESEND CURSOR P.SEL<SEL>
                  LET P.MSG<1> = ''
              ENDLOOP
              LET P.MSG<1> = P.MSG<2> = ''
              LET SEL = 0
          ENDIF
      ELSE
          LET SEL = 1
      ENDIF
  ENDLOOP
ENDPROC
