<<CCMEMSEL>> : A1=MEMBER NAME,  A2=LIB
  DECLARE PTR SEL PSWD PGMR DESC NAME BASE CARD LIBR SHOWPAN DSECT
  CREATE AWS LIBDATA NONRECOVERABLE LEVEL
  LET NAME = A1
  IF LENGTH(NAME) EQ 8 AND CONFORM(SUBSTR(NAME 7 2) 'N')
      LET BASE = SUBSTR(NAME 1 6)
  ELSE
      LET BASE = NAME
  ENDIF
  DO <<GETLIST>>
  IF AWSCOUNT GT 0
      LET SEL = 1
      IF AWSCOUNT GT 1 OR SHOWPAN EQ 'Y'
          DO <<SHOLIST>>
      ENDIF
      FETCH $$CCTEMP
     %READ AWS %SEL% CARD
      DELETE $$CCTEMP
  ELSE
      LET CARD = ''
  ENDIF
  LET CARD = PAD(CARD 80)
  LET NAME = TRIM(SUBSTR(CARD 1 8))
  LET PSWD = TRIM(SUBSTR(CARD 11 4))
  LET PGMR = SUBSTR(CARD 33 15)
  LET DESC = SUBSTR(CARD 49 30)
  IF LENGTH(NAME) EQ 8 AND CONFORM(SUBSTR(NAME 7 2) 'N')
      LET BASE = SUBSTR(NAME 1 6)
  ELSE
      LET BASE = NAME
  ENDIF
  DELETE
  RETURN NAME PSWD PGMR DESC BASE DSECT
<<GETLIST>> PROC
  LET LIBR = A2
  LET CARD = ' '
  LET SHOWPAN = 'N'
  SET MONTRAP ON
 %IMPORT DSN=%LIBR%,RANGE='%NAME%,%PAD(NAME 8 '9')%',LIB-I
  IF TC NE 0 AND LENGTH(BASE) EQ 6
      SET MONTRAP ON
     %IMPORT DSN=%LIBR%,RANGE='%BASE%,%PAD(BASE 8 '9')%',LIB-I
  ENDIF
  SET MONTRAP OFF
  LET SEL = 0
  LET DSECT = 'NODSECT'
  IF TC EQ 0
      DELETE 1
      SORT SORT FIELDS=(1,8,CH,D)
      RENUMBER 1 1
      UPDATE $$CCTEMP
      LOOP PTR FROM 1 TO HIGHSEQ BY 1
         %READ AWS %PTR% CARD
          LET CARD = SUBSTR(CARD 1 8)
          IF CARD EQ BASE|'D '
              LET DSECT = 'DSECT'
          ENDIF
          IF NOT CONFORM(SUBSTR(CARD 7 2) 'N') AND CARD NE PAD(A1 8)
             %DELETE %PTR%
          ENDIF
      ENDLOOP
      IF AWSCOUNT EQ 0
          LET SHOWPAN = 'Y'
          FETCH $$CCTEMP
      ENDIF
      RENUMBER 2 1
      EDIT 1 8
      TRAP
     %FIND /%PAD(A1 8)%/
      TRAP OFF
      IF TC EQ 0
         %COPY %SEQ% 1
         %DELETE %SEQ%
      ENDIF
      RENUMBER 1 1
      UPDATE $$CCTEMP
      LET SEL = 1
  ENDIF
ENDPROC
<<SHOLIST>> PROC
  PANEL ACTIVATE PFX.CCMEMPAN
 %LOOP PTR FROM 1 TO %HBOUND(P.SEL)% BY 1
    UNTIL PTR GT AWSCOUNT
     %READ AWS %PTR% CARD
      LET P.MEM<PTR>  = SUBSTR(CARD 1 8)
      LET P.UPD<PTR>  = SUBSTR(CARD 18 11)
      LET P.PGMR<PTR> = SUBSTR(CARD 33 15)
      LET P.DESC<PTR> = SUBSTR(CARD 49 30)
  ENDLOOP
  PANEL SEND CURSOR P.SEL<1>
  LOOP
    UNTIL AID EQ 'PF1' OR AID EQ 'CLEAR' OR AID EQ 'ENTER'
      LET P.MSG<1> = 'INVALID PF KEY - TRY AGAIN'
      PANEL RESEND CURSOR P.SEL<1>
      LET P.MSG<1> = ''
  ENDLOOP
  SELECT FIRST
    WHEN AID EQ 'PF1'
      EXEC PFX.CCHLPSHO 'CCMEM'
      SELECT AWS LIBDATA
      RELOOP
    WHEN AID EQ 'CLEAR'
      POP
      STOP
  ENDSEL
  LET SEL = 0
  LOOP
    UNTIL SEL GT 0
     %LOOP PTR FROM 1 TO %HBOUND(P.SEL)% BY 1
        UNTIL PTR GT AWSCOUNT
          LET A.SEL<PTR> = 'UALM'
          LET CARD = TRIM(TRIM(P.SEL<PTR> '_'))
          IF CARD NE ''
              IF CARD EQ 'S' OR CARD EQ 'X'
                  IF SEL NE 0
                      LET P.MSG<2> = 'ONLY ONE MEMBER MAY BE SELECTED'
                      LET A.SEL<PTR> = 'UAHM'
                  ENDIF
              ELSE
                  LET P.MSG<2> = 'SELECTION CHARACTER MUST'
                  LET P.MSG<2> = P.MSG<2>|' BE "S" OR "X"'
                  LET A.SEL<PTR> = 'UAHM'
              ENDIF
              LET SEL = PTR
          ENDIF
      ENDLOOP
      IF SEL GT 0
          IF A.SEL<SEL> EQ 'UAHM'
              PANEL RESEND CURSOR P.SEL<SEL>
              LOOP
                UNTIL AID EQ 'PF1' OR AID EQ 'CLEAR' OR AID EQ 'ENTER'
                  LET P.MSG<1> = 'INVALID PF KEY - TRY AGAIN'
                  PANEL RESEND CURSOR P.SEL<SEL>
                  LET P.MSG<1> = ''
              ENDLOOP
              LET P.MSG<1> = P.MSG<2> = ''
              LET SEL = 0
          ENDIF
      ELSE
          LET SEL = 1
      ENDIF
  ENDLOOP
ENDPROC
