<<$CCXL>> : A1=OPTION  A2=LISTNAME  <A3=REP/SUB>  <A4=REP/SUB>
  EXEC PFX.RESET
  DECLARE LISTNAME SAVENAME MEMB EMSG TOPPTR REPL SUBM
  DECLARE NSLOTS NMEMBS PTR CARD CURPOS NMSG RETINFO<4>
  TRAP
  DELETE $$CCLIST
  TRAP OFF
  LET TOPPTR = 0
  LET LISTNAME = A2
  IF A3 EQ 'REP' OR A4 EQ 'REP'
      LET REPL = 'Y'
  ELSE
      LET REPL = 'N'
  ENDIF
  IF A3 EQ 'SUB' OR A4 EQ 'SUB'
      LET SUBM = 'Y'
  ELSE
      LET SUBM = 'N'
  ENDIF
  DO <<EDITS>>
  LOOP
    UNTIL NMSG EQ 0 AND AID EQ 'PF6'
      DO <<PUTPAN>>
     %PANEL SEND CURSOR P.%CURPOS%
      LET CURPOS = ''
      IF AID EQ 'CLEAR'
          POP
          STOP
      ELSE
          DO <<GETPAN>>
          DO <<EDITS>>
          SELECT FIRST
            WHEN AID EQ 'PF1'
              EXEC PFX.CCHLPSHO 'CCXLX'
              SELECT AWS ROS1
            WHEN AID EQ 'PF2'
              EXEC PFX.CCDEFSET
              SELECT AWS ROS1
            WHEN AID EQ 'PF7'
              LET TOPPTR = TOPPTR - NSLOTS
              IF TOPPTR LT 0
                  LET TOPPTR = 0
              ENDIF
            WHEN AID EQ 'PF8'
              LET TOPPTR = TOPPTR + NSLOTS
              IF TOPPTR GE NMEMBS
                  LET TOPPTR = NMEMBS - 1
              ENDIF
          ENDSEL
      ENDIF
  ENDLOOP
  RETURN
<<GETPAN>> PROC
  LET LISTNAME = TRIM(TRIM(P.LISTNAME '_'))
  DELETE
  TRAP
  FETCH $$CCLIST
  TRAP OFF
 %LOOP PTR FROM 1 TO %NSLOTS% BY 1
      LET MEMB = TRIM(TRIM(P.MEMB<PTR> '_'))
      IF MEMB EQ ''
          TRAP
         %DELETE %TOPPTR+PTR%
          TRAP OFF
      ELSE
         %%TOPPTR+PTR% %MEMB%
      ENDIF
  ENDLOOP
  IF AWSCOUNT GT 0
      DO <<PREPLIST>>
      UPDATE $$CCLIST
  ENDIF
ENDPROC
<<PUTPAN>> PROC
  TRAP
  FETCH $$CCLIST
  TRAP OFF
  PANEL ACTIVATE PFX.CCXLXPAN
  SELECT FIRST
    WHEN A1 EQ 'A'
      LET P.TITLE = '   BMS MAP ASSEMBLE LIST PROCESSOR'
    WHEN A1 EQ 'B'
      LET P.TITLE = ' BATCH COBOL COMPILE LIST PROCESSOR'
    WHEN A1 EQ 'C'
      LET P.TITLE = '  CICS COBOL COMPILE LIST PROCESSOR'
    WHEN NONE
      LET P.TITLE = ' LIST PROCESSOR FOR UNKNOWN REASONS'
  ENDSEL
  LET NSLOTS = HBOUND(P.MEMB)
  LET P.LISTNAME = PAD(LISTNAME 8 '_')
  LOOP PTR FROM 1 TO NSLOTS BY 1
    UNTIL TOPPTR+PTR GT AWSCOUNT
     %READ AWS %TOPPTR+PTR% CARD
      LET P.MEMB<PTR> = PAD(TRIM(SUBSTR(PAD(CARD 8) 1 8)) 8 '_')
  ENDLOOP
  IF NMSG EQ 0
      LET P.MSG = 'PRESS "PF6" TO CONFIRM WHEN MEMBER LIST IS COMPLETE'
  ELSE
      LET P.MSG = EMSG
      LET EMSG = ''
      LET A.LISTNAME = 'UHM'
      LET CURPOS = 'LISTNAME'
      IF NMSG EQ 2
          LET A.MEMB<1> = 'UHM'
      ENDIF
  ENDIF
  IF CURPOS EQ ''
      LET CURPOS = 'LISTNAME'
  ENDIF
ENDPROC
<<EDITS>> PROC
  LET NMSG = 0
  IF LISTNAME EQ ''
      TRAP
      FETCH $$CCLIST
      TRAP OFF
      IF TC NE 0
          LET EMSG = 'ENTER EITHER A LIST NAME OR AT LEAST '
          LET EMSG = EMSG | 'ONE MEMBER NAME TO BE COMPILED'
          LET NMSG = 2
          LET CURPOS = 'LISTNAME'
      ENDIF
      LET SAVENAME = LISTNAME
  ELSE
      IF LISTNAME NE SAVENAME
          TRAP
         %FIND %LISTNAME%
          TRAP OFF
          IF TC EQ 0
              TRAP
              FETCH $$CCLIST
              TRAP OFF
             %COPY %LISTNAME% B
              LET SAVENAME = LISTNAME
          ELSE
             %LET EMSG = 'ROSCOE MEMBER "%LISTNAME%" NOT FOUND'
              LET NMSG = 1
              LET CURPOS = 'LISTNAME'
          ENDIF
      ENDIF
  ENDIF
  IF AWSCOUNT GT 0
      DO <<PREPLIST>>
      UPDATE $$CCLIST
  ENDIF
ENDPROC
<<PREPLIST>> PROC
  FILL 81 255 / /
  RENUMBER 1 1
  IF AWSCOUNT GT 1
     %EXEC PFX.IN 81 86 '000001' 1 1 %AWSCOUNT%
      SORT SORT FIELDS=(1,9,CH,A)
      EXEC PFX.DELDUPS 1 9
      SORT SORT FIELDS=(81,6,CH,A)
      FILL 81 86 / /
      RENUMBER 1 1
  ENDIF
  FILL 10 10 /C/
  LET NMEMBS = AWSCOUNT
ENDPROC
