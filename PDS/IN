<<IN>> : A1=FRCOL, A2=TOCOL, A3='START LIT', A4=INCR, A5=FR, A6=TO
:*--------------------------------------------------------------------*
:*  EXAMPLE:  IN 6 7 '01' 1 10 20                                     *
:*            WILL NUMBER CARDS 10 THRU 20 WITH 01, 02, 03...         *
:*            IN COLS 6-7                                             *
:*--------------------------------------------------------------------*
SET STMTCNT MAX
LET L1 = A1
LET L2 = A2 - A1 + 1
IF SUBSTR(LPAD(A3 L2) 1 1) EQ ' '
    LET L10 = ' '
ELSE
    IF SUBSTR(A3 LENGTH(A3) 1) EQ ' '
        LET L10 = '1'
    ELSE
        LET L10 = '0'
    ENDIF
ENDIF
LET L3 = INTEGER(A3)
LET L4 = A4
IF A5 EQ ''
    LET L5 = 1
    LET L6 = HIGHSEQ
ELSE
    LET L5 = INTEGER(A5)
    IF A6 EQ '' OR A6 GT HIGHSEQ
        LET L6 = HIGHSEQ
    ELSE
        LET L6 = INTEGER(A6)
    ENDIF
ENDIF
IF A7 EQ ''
    LET L7 = 1
ELSE
    LET L7 = INTEGER(A7)
ENDIF
<<LOOP>>
IF L5 GT L6
    RETURN
ENDIF
TRAP
+READ AWS +L5+ L8
TRAP OFF
IF TC NE 0
    LET L5 = L5 + L7
    GOTO <<LOOP>>
ENDIF
LET L8 = PAD(L8 100)
IF L10 EQ '1'
    LET L9 = PAD(L3 L2)
ELSE
    +LET L9 = LPAD(L3 L2 '+L10+')
ENDIF
IF LENGTH(L9) GT L2
    LET L9 = SUBSTR(L9 2)
    LET L3 = INTEGER(L9)
ENDIF
LET L8 = MODIFY(L8 L9 L1)
++L5+ +L8+
LET L3 = L3 + L4
LET L5 = L5 + L7
GOTO <<LOOP>>
