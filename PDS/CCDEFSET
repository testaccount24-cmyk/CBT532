<<CCDEFSET>> : A1=<NON BLANK = NO SCREEN DISPLAY IF EDITS OK>
  PUSH
  EXEC PFX.CCSESSET
  DECLARE NVAR NAWS PTR CARD CURPOS NMSG APID ENVR DONE STAT
  DECLARE VARS<26> DESC<26> MSGS<26> DEFS<26>
  LET NVAR = 26                     : NUMBER OF VARIABLE NAMES IN PANEL
  LET VARS<01> = 'APID'
  LET VARS<02> = 'ENVR'
  LET VARS<03> = 'SRCE'
  LET VARS<04> = 'SQLS'
  LET VARS<05> = 'MAPC'
  LET VARS<06> = 'URTS'
  LET VARS<07> = 'OLNK'
  LET VARS<08> = 'ITST'
  LET VARS<09> = 'BLNK'
  LET VARS<10> = 'BTST'
  LET VARS<11> = 'URTL'
  LET VARS<12> = 'ACBK'
  LET VARS<13> = 'ALOP'
  LET VARS<14> = 'AMOP'
  LET VARS<15> = 'BCOP'
  LET VARS<16> = 'BLOP'
  LET VARS<17> = 'BMOP'
  LET VARS<18> = 'CCOP'
  LET VARS<19> = 'CLOP'
  LET VARS<20> = 'CMOP'
  LET VARS<21> = 'JNAM'
  LET VARS<22> = 'ACCT'
  LET VARS<23> = 'ROOM'
  LET VARS<24> = 'UNAM'
  LET VARS<25> = 'MSGC'
  LET VARS<26> = 'REMT'
  TRAP
  SELECT AWS CCDEFS
  TRAP OFF
  IF TC NE 0
      CREATE AWS CCDEFS NONRECOVERABLE RPF EXPLICIT
  ENDIF
  FETCH PFX.$$CHDEFS
  DELETES 1 6 /      /
  RENUMBER 1 1
  LOOP PTR FROM 1 TO NVAR BY 1
      LET CARD = VARS<PTR> | ' ='
      TRAP
     %FIND '%CARD%' 1 %AWSCOUNT%
      IF TC EQ 0
         %READ AWS %SEQ% CARD
          LET DEFS<PTR> = TRIM(SUBSTR(CARD 8))
      ENDIF
  ENDLOOP
  TRAP
  FETCH $$CCDEFS
  TRAP OFF
  RENUMBER 1 1
  LET NAWS = AWSCOUNT
  SET BOUNDS AWS 1 6
  LOOP PTR FROM 1 TO NVAR BY 1
      LET CARD = VARS<PTR> | ' ='
      TRAP
     %FIND '%CARD%' 1 %NAWS%
      IF TC EQ 0
         %READ AWS %SEQ% CARD
          LET DESC<PTR> = TRIM(SUBSTR(CARD 8))
      ELSE
          LET NAWS = NAWS + 1
          %%NAWS% %VARS<PTR>%
      ENDIF
  ENDLOOP
  TRAP OFF
  SET BOUNDS AWS 1 72
  DO <<EDITS>>
  LET DONE = 'N'
  IF NMSG EQ 0
      LET STAT = 'OK'
      IF A1 NE ''
          LET DONE = 'Y'
      ENDIF
  ELSE
      LET STAT = 'NOT OK'
  ENDIF
  LOOP
    UNTIL DONE EQ 'Y'
      PANEL ACTIVATE PFX.CCDEFPAN
      DO <<PUTPAN>>
      IF CURPOS EQ ''
          LET CURPOS = 'APID'
      ENDIF
     %PANEL SEND CURSOR P.%CURPOS%
      LET CURPOS = ''
      IF AID EQ 'CLEAR'
          LET DONE = 'Y'
      ELSE
          DO <<GETPAN>>
          DO <<EDITS>>
          SELECT FIRST
            WHEN AID EQ 'PF6' AND NMSG EQ 0
              LOOP PTR FROM 1 TO NVAR BY 1
                  TRAP
                 %FIND '%VARS<PTR>%' 1 %NAWS%
                  IF TC NE 0
                      LET NAWS = NAWS + 1
                     %POINT AWS LINE %NAWS%
                  ENDIF
                 %%SEQ% %VARS<PTR>% = %DESC<PTR>%
              ENDLOOP
              TRAP OFF
              UPDATE $$CCDEFS /COMPILER DEFAULTS FOR CC/
              LET STAT = 'OK'
              LET DONE = 'Y'
            WHEN AID EQ 'PF1'
              EXEC PFX.CCHLPSHO 'CCDEF'
              SELECT AWS CCDEFS
          ENDSEL
      ENDIF
  ENDLOOP
  POP
  IF STAT EQ 'OK'
      RETURN
  ELSE
      RESPONSE 'ERRORS EXIST IN COMPILE OPTIONS;  CANNOT CONTINUE'
      DELETE
      DISCARD AWS CCDEFS
      SELECT AWS ROS1
      STOP
  ENDIF
<<GETPAN>> PROC
  LOOP PTR FROM 1 TO NVAR BY 1
     %LET CARD = P.%VARS<PTR>%
      LET CARD = TRIM(TRIM(CARD '_'))
:*--------------------------------------------------------------------*
:*    SELECT FIRST                                                    *
:*      WHEN VARS<PTR> EQ 'SRCE'                                      *
:*       %IF CARD EQ 'HLQ.%APID%.%ENVR%.LIBMAST'                      *
:*            LET CARD = ''                                           *
:*        ENDIF                                                       *
:*      WHEN VARS<PTR> EQ 'URTS'                                      *
:*       %IF CARD EQ 'HLQ.%APID%.%ENVR%.URTMAST'                     *
:*            LET CARD = ''                                           *
:*        ENDIF                                                       *
:*      WHEN VARS<PTR> EQ 'OLNK'                                      *
:*       %IF CARD EQ 'HLQ.%APID%.%ENVR%.ONLLOAD'                     *
:*            LET CARD = ''                                           *
:*        ENDIF                                                       *
:*      WHEN VARS<PTR> EQ 'BLNK'                                      *
:*       %IF CARD EQ 'HLQ.%APID%.%ENVR%.BATLOAD'                     *
:*            LET CARD = ''                                           *
:*        ENDIF                                                       *
:*    ENDSEL                                                          *
:*--------------------------------------------------------------------*
      LET DESC<PTR> = CARD
  ENDLOOP
  LET CARD = ''
ENDPROC
<<PUTPAN>> PROC
  LET NMSG = 0
  LOOP PTR FROM 1 TO NVAR BY 1
     %LET P.%VARS<PTR>% = PAD(DESC<PTR> MAXLEN(P.%VARS<PTR>%) '_')
      IF MSGS<PTR> NE ''
          LET NMSG = NMSG + 1
          IF NMSG LE HBOUND(P.MSG)
              LET P.MSG<NMSG> = MSGS<PTR>
          ENDIF
          IF INDEX(MSGS<PTR> 'DEFAULT' 1) LE 0
             %LET A.%VARS<PTR>% = 'UHM'
              IF CURPOS EQ ''
                  LET CURPOS = VARS<PTR>
              ENDIF
          ENDIF
      ENDIF
  ENDLOOP
  IF NMSG EQ 0
      LET P.MSG<1> = 'PRESS "PF6" TO CONFIRM ABOVE INFORMATION,'
      LET P.MSG<1> = P.MSG<1> | ' "CLEAR" TO DISCARD ANY CHANGES'
  ENDIF
  IF CURPOS EQ ''
      LET CURPOS = VARS<1>
  ENDIF
ENDPROC
<<EDITS>> PROC
  LOOP PTR FROM 1 TO NVAR BY 1
      LET MSGS<PTR> = ''
      SELECT FIRST
        WHEN VARS<PTR> EQ 'SRCE'
          SELECT FIRST
           %WHEN DESC<PTR> EQ 'HLQ.%APID%.%ENVR%.LIBMAST'
              LET DESC<PTR> = ''
            WHEN DESC<PTR> EQ ''
              LET MSGS<PTR> = 'SOURCE LIBRARY DEFAULTED'
          ENDSEL
        WHEN VARS<PTR> EQ 'MAPC'
          SELECT FIRST
           %WHEN DESC<PTR> EQ 'HLQ.%APID%.%ENVR%.ONLMAP'
              LET DESC<PTR> = ''
            WHEN DESC<PTR> EQ ''
              LET MSGS<PTR> = 'MAP COPYBOOK LIBRARY DEFAULTED'
          ENDSEL
        WHEN VARS<PTR> EQ 'URTS'
          SELECT FIRST
           %WHEN DESC<PTR> EQ 'HLQ.%APID%.%ENVR%.URTMAST'
              LET DESC<PTR> = ''
            WHEN DESC<PTR> EQ ''
              LET MSGS<PTR> = 'URT SOURCE LIBRARY DEFAULTED'
          ENDSEL
        WHEN VARS<PTR> EQ 'OLNK'
          SELECT FIRST
           %WHEN DESC<PTR> EQ 'HLQ.%APID%.%ENVR%.ONLLOAD'
              LET DESC<PTR> = ''
            WHEN DESC<PTR> EQ ''
              LET MSGS<PTR> = 'ONLINE LOAD LIBRARY DEFAULTED'
          ENDSEL
        WHEN VARS<PTR> EQ 'BLNK'
          SELECT FIRST
           %WHEN DESC<PTR> EQ 'HLQ.%APID%.%ENVR%.BATLOAD'
              LET DESC<PTR> = ''
            WHEN DESC<PTR> EQ ''
              LET MSGS<PTR> = 'BATCH LOAD LIBRARY DEFAULTED'
          ENDSEL
        WHEN VARS<PTR> EQ 'URTL'
          SELECT FIRST
           %WHEN DESC<PTR> EQ 'HLQ.%APID%.%ENVR%.BATLOAD'
              LET DESC<PTR> = ''
            WHEN DESC<PTR> EQ ''
              LET MSGS<PTR> = 'URT LOAD LIBRARY DEFAULTED'
          ENDSEL
      ENDSEL
  ENDLOOP
  LET NMSG = 0
  LET APID = ENVR = ''
  LOOP PTR FROM 1 TO NVAR BY 1
      SELECT FIRST
        WHEN VARS<PTR> EQ 'APID'
          IF LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'APPLICATION ID DEFAULTED'
          ENDIF
          IF INDEX('YYY XXX ' DESC<PTR>) GT 0
              LET APID = DESC<PTR>
          ELSE
              LET MSGS<PTR> = 'APPLICATION ID MUST BE "YYY" OR "XXX"'
          ENDIF
        WHEN VARS<PTR> EQ 'ENVR'
          IF LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'ENVIRONMENT DEFAULTED'
          ENDIF
          IF DESC<PTR> EQ 'DVL'
              LET ENVR = DESC<PTR>
          ELSE
              LET MSGS<PTR> = 'ONLY AN ENVIRONMENT OF "DVL" IS'
              LET MSGS<PTR> = MSGS<PTR> | ' CURRENTLY SUPPORTED'
          ENDIF
        WHEN VARS<PTR> EQ 'SRCE'
          IF DESC<PTR> EQ '' AND APID NE '' AND ENVR NE ''
             %LET DESC<PTR> = 'HLQ.%APID%.%ENVR%.LIBMAST'
          ENDIF
        WHEN VARS<PTR> EQ 'SQLS'
          SELECT FIRST
            WHEN LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'LOOK FOR SQL IN SOURCE DEFAULTED'
            WHEN DESC<PTR> NE 'Y' AND DESC<PTR> NE 'N'
              LET MSGS<PTR> = 'LOOK FOR SQL IN SOURCE'
              LET MSGS<PTR> = MSGS<PTR> | ' MUST BE "Y" OR "N"'
          ENDSEL
        WHEN VARS<PTR> EQ 'MAPC'
          IF DESC<PTR> EQ '' AND APID NE '' AND ENVR NE ''
             %LET DESC<PTR> = 'HLQ.%APID%.%ENVR%.ONLMAP'
          ENDIF
        WHEN VARS<PTR> EQ 'URTS'
          IF DESC<PTR> EQ '' AND APID NE '' AND ENVR NE ''
             %LET DESC<PTR> = 'HLQ.%APID%.%ENVR%.URTMAST'
          ENDIF
        WHEN VARS<PTR> EQ 'OLNK'
          IF DESC<PTR> EQ '' AND APID NE '' AND ENVR NE ''
             %LET DESC<PTR> = 'HLQ.%APID%.%ENVR%.ONLLOAD'
          ENDIF
        WHEN VARS<PTR> EQ 'ITST'
          SELECT FIRST
            WHEN LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'ONLINE INTERTEST SELECTION DEFAULTED'
            WHEN DESC<PTR> NE 'Y' AND DESC<PTR> NE 'N'
              LET MSGS<PTR> = 'ONLINE INTERTEST SELECTION'
              LET MSGS<PTR> = MSGS<PTR> | ' MUST BE "Y" OR "N"'
          ENDSEL
        WHEN VARS<PTR> EQ 'BLNK'
          IF DESC<PTR> EQ '' AND APID NE '' AND ENVR NE ''
             %LET DESC<PTR> = 'HLQ.%APID%.%ENVR%.BATLOAD'
          ENDIF
        WHEN VARS<PTR> EQ 'BTST'
          SELECT FIRST
            WHEN LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'BATCH INTERTEST SELECTION DEFAULTED'
            WHEN DESC<PTR> NE 'N'
              LET MSGS<PTR> = 'BATCH INTERTEST IS NOT YET AVAILABLE'
            WHEN DESC<PTR> NE 'Y' AND DESC<PTR> NE 'N'
              LET MSGS<PTR> = 'BATCH INTERTEST SELECTION'
              LET MSGS<PTR> = MSGS<PTR> | ' MUST BE "Y" OR "N"'
          ENDSEL
        WHEN VARS<PTR> EQ 'URTL'
          IF DESC<PTR> EQ '' AND APID NE '' AND ENVR NE ''
             %LET DESC<PTR> = 'HLQ.%APID%.%ENVR%.BATLOAD'
          ENDIF
        WHEN VARS<PTR> EQ 'ACBK'
          SELECT FIRST
            WHEN LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'MAP COPYBOOK DESTINATION DEFAULTED'
:971009:::::WHEN DESC<PTR> NE 'S' AND DESC<PTR> NE 'C'
            WHEN DESC<PTR> NE 'C'
              LET MSGS<PTR> = 'MAP COPYBOOK DESTINATION MUST BE'
              LET MSGS<PTR> = MSGS<PTR> | ' "C" (COPYLIB)   '
:971009:::::::LET MSGS<PTR> = MSGS<PTR> | ' "S" (SOURCE LIB)'
          ENDSEL
        WHEN VARS<PTR> EQ 'AMOP'
          IF LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'ASSEMBLER MODE OPTIONS DEFAULTED'
          ENDIF
        WHEN VARS<PTR> EQ 'BMOP'
          IF LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'BATCH COMPILE MODE OPTIONS DEFAULTED'
          ENDIF
        WHEN VARS<PTR> EQ 'CMOP'
          IF LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'CICS COMPILE MODE OPTIONS DEFAULTED'
          ENDIF
        WHEN VARS<PTR> EQ 'JNAM'
          SELECT FIRST
            WHEN LENGTH(DESC<PTR>) EQ 0
             %LET DESC<PTR> = 'D%S.PREFIX%'
              LET MSGS<PTR> = 'JOB NAME STUB DEFAULTED TO'
              LET MSGS<PTR> = MSGS<PTR> | ' "D" PLUS ROSCOE PREFIX'
            WHEN SUBSTR(DESC<PTR> 1 1) NE 'D'
              LET MSGS<PTR> = 'JOB NAME STUB MUST BEGIN WITH'
              LET MSGS<PTR> = MSGS<PTR> | ' "D" FOR DEVELOPMENT'
            WHEN LENGTH(DESC<PTR>) LT 3
              LET MSGS<PTR> = 'JOB NAME STUB MUST BE AT LEAST'
              LET MSGS<PTR> = MSGS<PTR> | ' 3 CHARACTERS LONG'
            WHEN NOT CONFORM(DESC<PTR> 'AN')
              LET MSGS<PTR> = 'CHARACTERS IN JOB NAME MUST'
              LET MSGS<PTR> = MSGS<PTR> | ' BE ALPHANUMERIC'
          ENDSEL
        WHEN VARS<PTR> EQ 'ACCT'
          SELECT FIRST
:*--------------------------------------------------------------------*
:*          %WHEN DESC<PTR> NE 'Y%APID%' AND APID EQ 'XXX'            *
:*           %LET DESC<PTR> = 'Y%APID%'                               *
:*           %LET MSGS<PTR> = 'ACCOUNT CODE DEFAULTED TO "Y%APID%"'   *
:*--------------------------------------------------------------------*
            WHEN LENGTH(DESC<PTR>) NE 4
              LET MSGS<PTR> = 'ACCOUNT CODE MUST CONTAIN 4 CHARACTERS'
          ENDSEL
        WHEN VARS<PTR> EQ 'ROOM'
          IF LENGTH(DESC<PTR>) EQ 0
              LET MSGS<PTR> = 'ROOM/BIN NUMBER IS MISSING'
          ENDIF
        WHEN VARS<PTR> EQ 'UNAM'
          IF LENGTH(DESC<PTR>) EQ 0
              LET CARD = SUBSTR(FORMLKEY INDEX(FORMLKEY '.')+1 11)
              IF INDEX(CARD '.') GT 0
                  LET CARD = SUBSTR(CARD 1 INDEX(CARD '.')-1)
              ENDIF
              LET DESC<PTR> = CARD
              LET MSGS<PTR> = 'USER NAME PARAMETER DEFAULTED'
          ENDIF
        WHEN VARS<PTR> EQ 'MSGC'
          IF LENGTH(DESC<PTR>) EQ 0
              LET DESC<PTR> = DEFS<PTR>
              LET MSGS<PTR> = 'MSGCLASS DEFAULTED'
          ENDIF
        WHEN VARS<PTR> EQ 'REMT'
          IF LENGTH(DESC<PTR>) EQ 0
              LET MSGS<PTR> = 'REMOTE PRINTER ID MISSING'
          ENDIF
      ENDSEL
      IF MSGS<PTR> NE ''
          LET NMSG = NMSG + 1
      ENDIF
  ENDLOOP
ENDPROC
