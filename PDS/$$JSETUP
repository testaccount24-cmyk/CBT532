<<JSETUP>> : A1=<NON BLANK = NO SCREEN DISPLAY IF CORRECT>
PUSH
DECLARE CARD PROJ SENDPFX ERRFLD PF6OK PTR MCLS --
        CAMODE CRMODE BAMODE BRMODE CCOP BCOP HOLD
EXEC PFX.RESET
LET SENDPFX = ERRFLD = PF6OK = ''
EXEC PFX.$$JCNVRT
CREATE AWS JSETUP NONRECOVERABLE LEVEL
SELECT AWS JSETUP
FETCH $$JPARMS
RENUMBER 1 1
PANEL ACTIVATE *
STARTDEF
TAG ! S
TAG @ SH
TAG \ UMH JNAM ACCT ROOM UNAM PRTR DIVN SRCE CLNK BLNK
TAG % SH  MSG
!                       @GATHER BASIC USER INFORMATION!

                  @(RUN THIS ANYTIME BY EXECUTING "$$JSETUP")!


!        JOB NAME:\________!     (default "DXXX....", XXX=Roscoe prefix)

!    ACCOUNT CODE:\____!         (first entry in the job accounting area)

!     ROOM NUMBER:\________!     (second entry in the job accounting area)

!       USER NAME:\___________!  (to appear in name area of job card)

!PREFERRED REMOTE:\________!     (to appear on the ROUTE card)

!        DIVISION:\___!          (e.g., ASD, ACS, etc.)

!  LIBRARIAN NAME:\__________________________________!

!  ONLINE LINKLIB:\__________________________________!

!   BATCH LINKLIB:\__________________________________!

%                                                                      !
ENDDEF
SET BOUNDS AWS 1 4
POINT AWS LINE 1
TRAP
FIND /JNAM/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET PROJ = TRIM(SUBSTR(CARD 9 4))
ELSE
    LET PROJ = ''
ENDIF
IF PROJ EQ ''
    LET PROJ = S.PREFIX
ENDIF
IF LENGTH(PROJ) LT 3
    LET PROJ = PAD(PROJ 2 'X')
ENDIF
IF LENGTH(PROJ) LT 4
    LET PROJ = PAD(PROJ 4 '.')
ENDIF
%LET P.JNAM = PAD('D%PROJ%' 8 '.')
POINT AWS LINE 1
TRAP
FIND /ACCT/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET HOLD = SUBSTR(CARD 8 4)
ELSE
    LET HOLD = ''
ENDIF
LET P.ACCT = HOLD
POINT AWS LINE 1
TRAP
FIND /ROOM/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET HOLD = SUBSTR(CARD 8 8)
ELSE
    LET HOLD = ''
ENDIF
LET P.ROOM = HOLD
POINT AWS LINE 1
TRAP
FIND /UNAM/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET HOLD = TRIM(SUBSTR(CARD 8))
ELSE
    LET HOLD = ''
ENDIF
IF HOLD EQ ''
    LET HOLD = SUBSTR(FORMLKEY INDEX(FORMLKEY '.')+1 11)
    IF INDEX(HOLD '.') GT 0
        LET HOLD = SUBSTR(HOLD 1 INDEX(HOLD '.')-1)
    ENDIF
ENDIF
LET P.UNAM = HOLD
POINT AWS LINE 1
TRAP
FIND /PRTR/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET HOLD = SUBSTR(CARD 8)
ELSE
    LET HOLD = ''
ENDIF
LET P.PRTR = HOLD
POINT AWS LINE 1
TRAP
FIND /SRCE/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET HOLD  = SUBSTR(CARD 8)
ELSE
    LET HOLD  = ''
ENDIF
LET P.SRCE  = HOLD
POINT AWS LINE 1
TRAP
FIND /DIVN/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET HOLD = SUBSTR(CARD 8)
ELSE
    LET HOLD = ''
ENDIF
IF LENGTH(HOLD) NE 3
    LET HOLD = 'ASD'
ENDIF
LET P.DIVN = HOLD
POINT AWS LINE 1
TRAP
FIND /CLNK/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET HOLD  = SUBSTR(CARD 8)
ELSE
    LET HOLD  = ''
ENDIF
LET P.CLNK  = HOLD
POINT AWS LINE 1
TRAP
FIND /BLNK/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET HOLD  = SUBSTR(CARD 8)
ELSE
    LET HOLD  = ''
ENDIF
LET P.BLNK  = HOLD
POINT AWS LINE 1
TRAP
FIND /MCLS/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET MCLS  = SUBSTR(CARD 8)
ELSE
    LET MCLS  = 'S'
ENDIF
POINT AWS LINE 1
TRAP
FIND /CCOP/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET CCOP  = SUBSTR(CARD 8)
ELSE
    LET CCOP  = 'OPT'
ENDIF
POINT AWS LINE 1
TRAP
FIND /BCOP/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET BCOP  = SUBSTR(CARD 8)
ELSE
    LET BCOP  = 'OPT'
ENDIF
POINT AWS LINE 1
TRAP
FIND /CLOP/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET CAMODE  = SUBSTR(CARD 14 2)
    LET CRMODE  = SUBSTR(CARD 23)
ENDIF
IF CAMODE NE '31' AND CAMODE NE '24'
    LET CAMODE = '31'
ENDIF
IF CRMODE EQ 'ANY'
    IF CAMODE NE '31'
        LET CRMODE = '24'
    ENDIF
ELSE
    IF CRMODE NE '24'
        IF CAMODE EQ '31'
            LET CRMODE = 'ANY'
        ELSE
            LET CRMODE = '24'
        ENDIF
    ENDIF
ENDIF
POINT AWS LINE 1
TRAP
FIND /BLOP/
IF TC EQ 0
    %READ AWS %SEQ% CARD
    LET BAMODE  = SUBSTR(CARD 14 2)
    LET BRMODE  = SUBSTR(CARD 23)
ENDIF
IF BAMODE NE '31' AND BAMODE NE '24'
    LET BAMODE = '31'
ENDIF
IF BRMODE EQ 'ANY'
    IF BAMODE NE '31'
        LET BRMODE = '24'
    ENDIF
ELSE
    IF BRMODE NE '24'
        IF BAMODE EQ '31'
            LET BRMODE = 'ANY'
        ELSE
            LET BRMODE = '24'
        ENDIF
    ENDIF
ENDIF
<<DOEDITS>>
LET P.JNAM = LTRIM(TRIM(TRIM(TRIM(P.JNAM '.') '_')))
IF LENGTH(P.JNAM) LT 1
    LET P.JNAM = 'D'
ENDIF
IF LENGTH(P.JNAM) LT 3
    %LET P.JNAM = '%P.JNAM%%S.PREFIX%'
ENDIF
IF LENGTH(P.JNAM) LT 8
    LET P.JNAM = PAD(P.JNAM 8 '.')
ENDIF
IF SUBSTR(P.JNAM 1 1) NE 'D'
    LET P.MSG = 'JOB NAME MUST BEGIN WITH "D" (DEVELOPMENT)'
    LET ERRFLD   = 'P.JNAM'
    GOTO <<PANSEND>>
ENDIF
LET PROJ = UPCASE(SUBSTR(P.JNAM 2 4))
IF NOT CONFORM(SUBSTR(PROJ 1 2) 'AN')
    LET P.MSG = 'JOB NAME CHARACTERS 2 TO 3 MUST BE ALPHANUMERIC'
    LET ERRFLD   = 'P.JNAM'
    GOTO <<PANSEND>>
ENDIF
LET P.ACCT = LTRIM(TRIM(TRIM(P.ACCT '_')))
IF LENGTH(P.ACCT) NE 4
    LET P.ACCT = PAD(P.ACCT 4 '_')
    LET P.MSG = 'ACCOUNT CODE MISSING OR INCOMPLETE'
    LET ERRFLD   = 'P.ACCT'
    GOTO <<PANSEND>>
ENDIF
LET P.ROOM = LTRIM(TRIM(TRIM(P.ROOM '_')))
IF LENGTH(P.ROOM) EQ 0
    LET P.MSG = 'ROOM NUMBER MISSING'
    LET ERRFLD   = 'P.ROOM'
    GOTO <<PANSEND>>
ENDIF
LET P.UNAM = LTRIM(TRIM(TRIM(P.UNAM '_')))
IF P.UNAM EQ ''
    LET P.UNAM = SUBSTR(FORMLKEY INDEX(FORMLKEY '.')+1 11)
    IF INDEX(P.UNAM '.') GT 0
        LET P.UNAM = SUBSTR(P.UNAM 1 INDEX(P.UNAM '.')-1)
    ENDIF
ENDIF
LET P.PRTR = LTRIM(TRIM(TRIM(P.PRTR '_')))
IF LENGTH(P.PRTR) EQ 0
    LET P.MSG = 'REMOTE PRINTER ID MISSING'
    LET ERRFLD   = 'P.PRTR'
    GOTO <<PANSEND>>
ENDIF
LET P.DIVN = LTRIM(TRIM(TRIM(P.DIVN '_')))
IF LENGTH(P.DIVN) EQ 0
    LET P.DIVN = 'ASD'
ELSE
    IF LENGTH(P.DIVN) NE 3
        LET P.MSG = 'DIV MUST BE 3 CHARACTERS (XXX, YYY, ETC)'
        LET ERRFLD   = 'P.DIVN'
        GOTO <<PANSEND>>
    ENDIF
ENDIF
LET P.SRCE = LTRIM(TRIM(TRIM(P.SRCE '_')))
IF P.SRCE EQ 'D'
    LET P.SRCE = 'DVL.SOURCE.LIBMAST'
ENDIF
IF P.SRCE EQ 'S'
    LET P.SRCE = 'HLQ.SOURCE.LIBMAST'
ENDIF
IF P.SRCE EQ 'P'
    LET P.SRCE = 'HLQ.XXX.PRD.LIBMAST'
ENDIF
IF LENGTH(P.SRCE) EQ 0
    LET P.MSG = 'SOURCE LIBRARY NAME MISSING (D=DVL, S=SAT, P=PROD)'
    LET ERRFLD   = 'P.SRCE'
    GOTO <<PANSEND>>
ENDIF
LET P.CLNK = LTRIM(TRIM(TRIM(P.CLNK '_')))
IF LENGTH(P.CLNK) EQ 0
    LET P.MSG = 'ONLINE LINK LIBRARY NAME MISSING'
    LET ERRFLD   = 'P.CLNK'
    GOTO <<PANSEND>>
ENDIF
LET P.BLNK = LTRIM(TRIM(TRIM(P.BLNK '_')))
IF LENGTH(P.BLNK) EQ 0
    LET P.MSG = 'BATCH LINK LIBRARY NAME MISSING'
    LET ERRFLD   = 'P.BLNK'
    GOTO <<PANSEND>>
ENDIF
IF A1 EQ ''
    IF AID EQ 'PF6' AND PF6OK EQ 'YES'
        GOTO <<SAVEALL>>
    ENDIF
    LET P.MSG = 'PRESS "PF6" TO SAVE ABOVE ENTRIES, "CLEAR" TO DISCARD'
    LET PF6OK = 'YES'
    LET ERRFLD = ''
ELSE
    GOTO <<SAVEALL>>
ENDIF
<<PANSEND>>
LET P.ACCT = PAD(P.ACCT 4 '_')
LET P.ROOM = PAD(P.ROOM 8 '_')
LET P.UNAM = PAD(P.UNAM 11 '_')
LET P.PRTR = PAD(P.PRTR 8 '_')
LET P.SRCE = PAD(P.SRCE 34 '_')
LET P.CLNK = PAD(P.CLNK 34 '_')
LET P.BLNK = PAD(P.BLNK 34 '_')
IF ERRFLD EQ ''
    LET ERRFLD = 'P.JNAM'
ELSE
    LET PF6OK = 'NO'
ENDIF
%PANEL %SENDPFX%SEND CURSOR %ERRFLD%
IF AID EQ 'CLEAR'
    GOTO <<RETURN>>
ENDIF
LET ERRFLD = ''
LET SENDPFX = 'RE'
LET P.MSG = ' '
GOTO <<DOEDITS>>
<<SAVEALL>>
DELETE
%01 JNAM = %TRIM(P.JNAM '.')%
%02 ACCT = %P.ACCT%
%03 ROOM = %P.ROOM%
%04 UNAM = %P.UNAM%
%05 MCLS = %MCLS%
%06 PRTR = %P.PRTR%
%07 DIVN = %P.DIVN%
%08 SRCE = %P.SRCE%
%09 CCOP = %CCOP%
%10 BCOP = %BCOP%
%11 CLNK = %P.CLNK%
%12 BLNK = %P.BLNK%
%13 CLOP = AMODE=%CAMODE%,RMODE=%CRMODE%
%14 BLOP = AMODE=%BAMODE%,RMODE=%BRMODE%
UPDATE $$JPARMS
<<RETURN>>
DELETE
DISCARD AWS JSETUP
POP
RETURN
