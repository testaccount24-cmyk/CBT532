<<CCURTSEL>> : A1=URT NAME,  A2=LIB
  DECLARE PTR SEL NAME BASE CARD LIBR
  CREATE AWS LIBDATA NONRECOVERABLE LEVEL
  LET NAME = A1
  IF LENGTH(NAME) GT 5
      LET BASE = SUBSTR(NAME 1 6)
  ELSE
      LET BASE = NAME
  ENDIF
  DO <<GETLIST>>
  IF AWSCOUNT GT 0
      LET SEL = 1
      IF AWSCOUNT GT 1
          DO <<SHOLIST>>
      ENDIF
      FETCH $$CCTEMP
     %READ AWS %SEL% CARD
      DELETE $$CCTEMP
  ELSE
      LET CARD = ''
  ENDIF
  LET CARD = PAD(CARD 80)
  LET NAME = TRIM(SUBSTR(CARD 1 8))
  DELETE
  RETURN NAME
<<GETLIST>> PROC
  LET LIBR = A2
  LET CARD = ' '
  SET MONTRAP ON
 %IMPORT DSN=%LIBR%(*),RANGE='%BASE%,%PAD(BASE 8 '9')%'
  SET MONTRAP OFF
  LET SEL = 0
  IF TC EQ 0
      TRAP ON
      DELETEX 1 9 /RT /
      TRAP OFF
      IF AWSCOUNT GT 0
          RENUMBER 1 1
          UPDATE $$CCTEMP
          LET SEL = 1
      ENDIF
  ENDIF
ENDPROC
<<SHOLIST>> PROC
  PANEL ACTIVATE PFX.CCURTPAN
 %LOOP PTR FROM 1 TO %HBOUND(P.SEL)% BY 1
    UNTIL PTR GT AWSCOUNT
     %READ AWS %PTR% CARD
      LET P.URT<PTR>  = SUBSTR(CARD 1 8)
      LET P.UPD<PTR>  = SUBSTR(CARD 18 11)
      LET P.PGMR<PTR> = SUBSTR(CARD 33 15)
      LET P.DESC<PTR> = SUBSTR(CARD 49 30)
  ENDLOOP
  PANEL SEND CURSOR P.SEL<1>
  SELECT FIRST
    WHEN AID EQ 'PF1'
      EXEC PFX.CCHLPSHO 'CCURT'
      SELECT AWS LIBDATA
      RELOOP
    WHEN AID EQ 'CLEAR'
      POP
      STOP
  ENDSEL
  LET SEL = 0
  LOOP
    UNTIL SEL GT 0
     %LOOP PTR FROM 1 TO %HBOUND(P.SEL)% BY 1
        UNTIL PTR GT AWSCOUNT
          LET A.SEL<PTR> = 'UALM'
          LET CARD = TRIM(TRIM(P.SEL<PTR> '_'))
          SELECT FIRST
            WHEN CARD EQ 's'
              LET CARD = 'S'
            WHEN CARD EQ 'x'
              LET CARD = 'X'
          ENDSEL
          IF CARD NE ''
              IF CARD EQ 'S' OR CARD EQ 'X'
                  IF SEL NE 0
                      LET P.MSG<2> = 'ONLY ONE URT MAY BE SELECTED'
                      LET A.SEL<PTR> = 'UAHM'
                  ENDIF
              ELSE
                  LET P.MSG<2> = 'SELECTION CHARACTER MUST'
                  LET P.MSG<2> = P.MSG<2>|' BE "S" OR "X"'
                  LET A.SEL<PTR> = 'UAHM'
              ENDIF
              LET SEL = PTR
          ENDIF
      ENDLOOP
      IF SEL GT 0
          IF A.SEL<SEL> EQ 'UAHM'
              PANEL RESEND CURSOR P.SEL<SEL>
              LET P.MSG<1> = P.MSG<2> = ''
              LET SEL = 0
          ENDIF
      ELSE
          LET SEL = 1
      ENDIF
  ENDLOOP
ENDPROC
