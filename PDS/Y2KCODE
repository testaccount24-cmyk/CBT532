<<Y2KCODE>>
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:   VARIABLE USAGE
:       L1 - HOLDS 3 CHARACRTER DATABASE TABLE NAME
:       L2 - HOLDS DATE FIELD NAME
:       L3 - HOLDS ROSCOE MEMBER NAME TO SAVE OUTPUT UNDER
:       L4 - HOLDS PANEL FIELD NAME FOR CURSOR POSITIONING
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
  PUSH
  DO <<PARANAME>>
<<MAINLOOP>>
  DO <<PANEL>>
<<MAINLOP1>>
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:   CLEAR AWS AND EXIT
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
IF (S.AID EQ 'PF3')
    DELETE
    DELETE @UNPK
    DELETE @PACK
    GOTO <<END>>
ENDIF
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:   MAKE PARAGRAPH EXIT, CREATE MEMBER, STARTOVER
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
IF (S.AID EQ 'PF4')
    DO <<MAKEXIT>>
    GOTO <<MAINLOOP>>
ENDIF
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
: CHECK IF INVALID KEY PRESSED
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
IF (S.AID NE 'ENTER')
    GOTO <<MAINLOOP>>
ENDIF
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
: MUST BE THE ENTER KEY, MAKE AN IF STATEMENT
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
LET L4 = 'DATFORM'
IF (P.DATFORM LT '1') OR (P.DATFORM GT '4')
+   PANEL RESEND CURSOR P.+L4+
    GOTO <<MAINLOP1>>
ENDIF
<<CONT1>>
LET L4 = 'DATFLD'
IF (S.AID EQ 'ENTER')
    DO <<MAKEIF>>
    LET P.DATFLD = '__________________________________'
+   PANEL RESEND CURSOR P.+L4+
    GOTO <<MAINLOP1>>
ENDIF
<<END>>
POP
RETURN
:
:
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:
<<MAKEIF>> PROC      : MAKE AN IF STATEMENT AT BOTTOM OF AWS
    IF (P.DATFORM EQ '1')
        DO <<YYMMDD>>
    ENDIF
    IF (P.DATFORM EQ '2')
        DO <<YYMM>>
    ENDIF
    IF (P.DATFORM EQ '3')
        DO <<YY>>
    ENDIF
    IF (P.DATFORM EQ '4')
        DO <<YYDDD>>
    ENDIF
ENDPROC
:
:
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:
<<MAKEXIT>> PROC     : MAKE A PARAGRAPH EXIT BOTTOM OF AWS
:                      ONE FOR UNPACK, ONE FOR PACK
    FETCH @UNPK
    WRITE AWS B
'       9000-UNPACK-###-DATES-EXIT.'
'           EXIT.'
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
+   EDIT /###/+L1+/
+   LET L3 = 'UNPK+L1+'
+   UPDATE +L3+
:
    FETCH @PACK
    WRITE AWS B
'       9000-PACK-###-DATES-EXIT.'
'           EXIT.'
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
+   EDIT /###/+L1+/
+   LET L3 = 'PACK+L1+'
+   UPDATE +L3+
    DEL @UNPK
    DEL @PACK
    DO <<PARANAME>>
ENDPROC
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:
<<PANEL>> PROC       : DISPLAY THE PANEL
    PANEL ACTIVATE *
    STARTDEF
    TAG # (S GREEN)
    TAG & (UM RED) DBTBL DATFORM DATFLD
    TAG @ (S BLUE)
    TAG % (S YELLOW)
#      COBOL CODE GENERATOR FOR PACKING AND UNPACKING YEAR 2000 DATES
#
#
#        DATABASE TABLE NAME:&___#
#
#   ENTER DATE FORMAT NUMBER:&_#
#
@                             1 - YYMMDD
@                             2 - YYMM
@                             3 - YY
@                             4 - YYDDD
#
#            DATE FIELD NAME:&__________________________________#
#
#
#
#
#
#
#
#
#
#
#   %PF3=EXIT    %PF4=SAVE CODE      %ENTER=GENERATE CODE
    ENDDEF
    LET P.DBTBL = '___'
    LET P.DATFLD = '__________________________________'
    LET P.DATFORM = '1'
    PANEL SEND
ENDPROC
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
<<PARANAME>> PROC    : PUT THE FIRST PART IN THE AWS
:                      ONE FOR UNPACK, ONE FOR PACK
    DEL
    WRITE AWS T
'       9000-UNPACK-###-DATES.'
'      *--------------------------------------------------------------*'
'      * INCLUDE FILE:  UNPACK-###-DATES                              *'
'      *--------------------------------------------------------------*'
'      * THIS PROCEDURE UNPACKS Y2K DATES IN THE ### RECORD.          *'
'      *--------------------------------------------------------------*'
'      *--------------------------------------------------------------*'
'      * THIS PROCEDURE WILL BE PERFORMED EACH TIME THE PROGRAM READS *'
'      * A ### RECORD.  IT MOVES EACH DATE IN THE ### RECORD A WORK   *'
'      * AREA.  PERFORMS THE Y2K DATE CONVERSION, WHICH RETURNS A     *'
'      * STANDARD ACS SIX CHARACTER DATE (YYMMDD). THEN IT OVERLAYS   *'
'      * THE Y2K DATE IN THE ### RECORD WITH THE UNPACKED DATE.       *'
'      *--------------------------------------------------------------*'
    ENDWRITE
    UPDATE @UNPK
:
    DEL
    WRITE AWS T
'       9000-PACK-###-DATES.'
'      *--------------------------------------------------------------*'
'      * INCLUDE FILE:  PACK-###-DATES                                *'
'      *--------------------------------------------------------------*'
'      * THIS PROCEDURE PACKS Y2K DATES IN THE ### RECORD.            *'
'      *--------------------------------------------------------------*'
'      *--------------------------------------------------------------*'
'      * THIS PROCEDURE WILL BE PERFORMED EACH TIME THE PROGRAM WRITES*'
'      * A ### RECORD.  IT MOVES EACH DATE IN THE ### RECORD TO A     *'
'      * WORK AREA.  PERFORMS THE Y2K DATE CONVERSION, WHICH RETURNS  *'
'      * RETURNS A STANDARD ACS SIX CHARACTER PACKED DATE.  THEN IT   *'
'      * OVERLAYS THE DATE IN THE ### RECORD WITH THE PACKED DATE.    *'
'      *--------------------------------------------------------------*'
    ENDWRITE
    UPDATE @PACK
    DEL
ENDPROC
:
:
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:
<<YYMMDD>> PROC      : MAKE AN IF STATEMENT AT BOTTOM OF AWS
    FETCH @UNPK
    WRITE AWS B
'      *--------------------------------------------------------------*'
'      * UNPACK SIX CHARACTER DATE: (YYMMDD)                          *'
'      *--------------------------------------------------------------*'
'      * UNPACK: ###01-@@@                                            *'
'      *--------------------------------------------------------------*'
'           IF ###01-@@@(1:1) = HIGH-VALUES'
'               MOVE ###01-@@@ TO Y2K-PACKED-DATE'
'               MOVE Y2K-PACKED-CYMD TO Y2K-UNPACKED-DATE'
'               MOVE Y2K-UNPACKED-YMD TO ###01-@@@'
'           END-IF.'
' '
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
    LET L2 = TRIM(P.DATFLD '_')
+   EDIT /###/+L1+/
+   EDIT /@@@/+L2+/
    UPDATE @UNPK
:
    FETCH @PACK
    WRITE AWS B
'      *--------------------------------------------------------------*'
'      * PACK SIX CHARACTER DATE: (YYMMDD)                            *'
'      *--------------------------------------------------------------*'
'      * PACK: ###01-@@@                                              *'
'      *--------------------------------------------------------------*'
'           MOVE ###01-@@@ TO Y2K-UNPACKED-YMD.'
'           IF Y2K-YEAR'
'              MOVE HIGH-VALUES       TO Y2K-CHECK-BYTE'
'              MOVE THE-21ST-CENTURY  TO Y2K-UNPACKED-CENTURY'
'              MOVE Y2K-UNPACKED-DATE TO Y2K-PACKED-CYMD'
'              MOVE Y2K-PACKED-DATE   TO ###01-@@@'
'           END-IF.'
' '
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
    LET L2 = TRIM(P.DATFLD '_')
+   EDIT /###/+L1+/
+   EDIT /@@@/+L2+/
    UPDATE @PACK
ENDPROC
:
:
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:
<<YYMM>> PROC      : MAKE AN IF STATEMENT AT BOTTOM OF AWS
    FETCH @UNPK
    WRITE AWS B
'      *--------------------------------------------------------------*'
'      * UNPACK FOUR CHARACTER DATE: (YYMM)                           *'
'      *--------------------------------------------------------------*'
'      * UNPACK: ###01-@@@                                            *'
'      *--------------------------------------------------------------*'
'           IF ###01-@@@(1:1) = HIGH-VALUES'
'               MOVE ###01-@@@ TO Y2K-4-DIGIT'
'               MOVE Y2K-4DIG-PACKED TO Y2K-4DIG-UNPACKED'
'               MOVE Y2K-UNPACKED-YM TO ###01-@@@'
'           END-IF.'
' '
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
    LET L2 = TRIM(P.DATFLD '_')
+   EDIT /###/+L1+/
+   EDIT /@@@/+L2+/
    UPDATE @UNPK
:
    FETCH @PACK
    WRITE AWS B
'      *--------------------------------------------------------------*'
'      * PACK FOUR CHARACTER DATES (YYMM)                             *'
'      *--------------------------------------------------------------*'
'      * PACK: ###01-@@@                                              *'
'      *--------------------------------------------------------------*'
'           MOVE ###01-@@@ TO Y2K-UNPACKED-YM.'
'           IF Y2K-4DIG-YEAR'
'               MOVE HIGH-VALUES       TO Y2K-4DIG-CHECK'
'               SET Y2K-4DIG-CENTURY   TO TRUE'
'               MOVE Y2K-4DIG-UNPACKED TO Y2K-4DIG-PACKED'
'               MOVE Y2K-4-DIGIT       TO ###01-@@@'
'           END-IF.'
' '
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
    LET L2 = TRIM(P.DATFLD '_')
+   EDIT /###/+L1+/
+   EDIT /@@@/+L2+/
    UPDATE @PACK
ENDPROC
:
:
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:
<<YY>> PROC      : MAKE AN IF STATEMENT AT BOTTOM OF AWS
    FETCH @UNPK
    WRITE AWS B
'      *--------------------------------------------------------------*'
'      * UNPACK Y2K YEAR: (YY)                                        *'
'      *--------------------------------------------------------------*'
'      * UNPACK: ###01-@@@                                            *'
'      *--------------------------------------------------------------*'
'           MOVE ###01-@@@ TO DB-FY-BYTES.'
'           IF 1ST-DECADE'
'             MOVE ''0'' TO FY-1STCHAR.'
'           IF 2ND-DECADE'
'             MOVE ''1'' TO FY-1STCHAR.'
'           IF 3RD-DECADE'
'             MOVE ''2'' TO FY-1STCHAR.'
'           IF 4TH-DECADE'
'             MOVE ''3'' TO FY-1STCHAR.'
'           MOVE DB-FY-BYTES TO ###01-@@@.'
' '
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
    LET L2 = TRIM(P.DATFLD '_')
+   EDIT /###/+L1+/
+   EDIT /@@@/+L2+/
    UPDATE @UNPK
:
    FETCH @PACK
    WRITE AWS B
'      *--------------------------------------------------------------*'
'      * PACK Y2K YEAR: (YY)                                          *'
'      *--------------------------------------------------------------*'
'      * PACK: ###01-@@@                                              *'
'      *--------------------------------------------------------------*'
'           MOVE ###01-@@@ TO DB-FY-BYTES.'
'           IF FY-1STCHAR = ''0'''
'             SET 1ST-DECADE TO TRUE.'
'           IF FY-1STCHAR = ''1'''
'             SET 2ND-DECADE TO TRUE.'
'           IF FY-1STCHAR = ''2'''
'             SET 3RD-DECADE TO TRUE.'
'           IF FY-1STCHAR = ''3'''
'             SET 4TH-DECADE TO TRUE.'
'           MOVE DB-FY-BYTES TO ###01-@@@.'
' '
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
    LET L2 = TRIM(P.DATFLD '_')
+   EDIT /###/+L1+/
+   EDIT /@@@/+L2+/
    UPDATE @PACK
ENDPROC
:
:
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:
<<YYDDD>> PROC      : MAKE AN IF STATEMENT AT BOTTOM OF AWS
    FETCH @UNPK
    WRITE AWS B
'      *--------------------------------------------------------------*'
'      * UNPACK FIVE CHARACTER DATES: (YYDDD)                         *'
'      *--------------------------------------------------------------*'
'      * UNPACK: ###01-@@@                                            *'
'      *--------------------------------------------------------------*'
'           IF ###01-@@@(1:1) = HIGH-VALUES'
'               MOVE ###01-@@@       TO Y2K-5-DIGIT'
'               MOVE Y2K-5DIG-PACKED TO Y2K-5DIG-UNPACKED'
'               MOVE Y2K-UNPACKED-YYDDD TO ###01-@@@'
'           END-IF.'
' '
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
    LET L2 = TRIM(P.DATFLD '_')
+   EDIT /###/+L1+/
+   EDIT /@@@/+L2+/
    UPDATE @UNPK
:
    FETCH @PACK
    WRITE AWS B
'      *--------------------------------------------------------------*'
'      * PACK FIVE CHARACTER DATES: (YYDDD)                           *'
'      *--------------------------------------------------------------*'
'      * PACK: ###01-@@@                                              *'
'      *--------------------------------------------------------------*'
'           MOVE ###01-@@@ TO Y2K-UNPACKED-YYDDD.'
'           IF Y2K-5DIG-YEAR'
'              MOVE HIGH-VALUES       TO Y2K-5DIG-CHECK'
'              SET Y2K-5DIG-CENTURY   TO TRUE'
'              MOVE Y2K-5DIG-UNPACKED TO Y2K-5DIG-PACKED'
'              MOVE Y2K-5-DIGIT       TO ###01-@@@'
'           END-IF.'
' '
    ENDWRITE
    LET L1 = TRIM(P.DBTBL '_')
    LET L2 = TRIM(P.DATFLD '_')
+   EDIT /###/+L1+/
+   EDIT /@@@/+L2+/
    UPDATE @PACK
ENDPROC
